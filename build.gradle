apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.7

group = 'org.squiddev'
version = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'describe', '--always', '--tags'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}()

configurations {
	deployerJars
}

repositories {
	mavenCentral()
}

dependencies {
	compile group: 'org.ow2.asm', name: 'asm-all', version: '5.+'
	compile group: 'org.luaj', name: 'luaj-jse', version: '2.+'

	testCompile group: 'junit', name: 'junit', version: '4.+'

	deployerJars "org.apache.maven.wagon:wagon-ftp:2.2"
}

task sourcesJar(type: Jar, dependsOn:classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives jar

	archives sourcesJar
	archives javadocJar
}

def isTag = {
	def stdout = new ByteArrayOutputStream()
	def error = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'describe', '--tag'
		ignoreExitValue = true
		errorOutput = error
		standardOutput = stdout
	}
	return error.toString().trim() == "" && stdout.toString().trim != ""
}

uploadArchives {
	repositories {
		if ((project.hasProperty("ftpHost") || System.getenv('FTP_USERNAME') != null) && isTag()) {
			mavenDeployer {
				configuration = configurations.deployerJars
				repository(url: ftpHost) {
					authentication(
							userName: project.hasProperty("ftpHost") ? ftpUser : System.getenv('FTP_USERNAME'),
							password: project.hasProperty("ftpPass") ? ftpPass : System.getenv('FTP_PASSWORD')
					)
				}

				pom.project {
					name 'luaj.api'
					packaging 'jar'
					description 'A helpful API wrapper for LuaJ '
					url 'https://github.com/SquidDev/luaj.api'

					scm {
						url 'scm:git@github.com:SquidDev/luaj.api.git'
						connection 'scm:git@github.com:SquidDev/luaj.api.git'
						developerConnection 'scm:git@github.com:SquidDev/luaj.api.git'
					}

					licenses {
						license {
							name 'MIT License'
							url 'http://opensource.org/licenses/mit-license.php'
							distribution 'repo'
						}
					}

					developers {
						developer {
							id 'SquidDev'
							name 'SquidDev'
						}
					}
				}
			}
		} else {
			mavenLocal()
		}
	}
}
