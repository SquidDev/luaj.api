apply plugin: 'java'
apply plugin: 'maven'

sourceCompatibility = 1.7

group = 'org.squiddev'
def deploy = new Deploy(project: project)
version = deploy.toString()

configurations {
	deployerJars
}

repositories {
	mavenCentral()
}

dependencies {
	compile group: 'org.ow2.asm', name: 'asm-all', version: '5.+'
	compile group: 'org.luaj', name: 'luaj-jse', version: '2.+'

	testCompile group: 'junit', name: 'junit', version: '4.+'

	deployerJars "org.apache.maven.wagon:wagon-ftp:2.2"
}

task sourcesJar(type: Jar, dependsOn:classes) {
	classifier = 'sources'
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
	classifier = 'javadoc'
	from javadoc.destinationDir
}

artifacts {
	archives jar

	archives sourcesJar
	archives javadocJar
}

uploadArchives {
	repositories {
		if (deploy.shouldDeploy()) {
			mavenDeployer {
				configuration = configurations.deployerJars
				repository(url: deploy.getHost()) {
					authentication(userName: deploy.getUser(), password: deploy.getPass())
				}

				pom.project {
					name 'luaj.api'
					packaging 'jar'
					description 'A helpful API wrapper for LuaJ '
					url 'https://github.com/SquidDev/luaj.api'

					scm {
						url 'scm:git@github.com:SquidDev/luaj.api.git'
						connection 'scm:git@github.com:SquidDev/luaj.api.git'
						developerConnection 'scm:git@github.com:SquidDev/luaj.api.git'
					}

					licenses {
						license {
							name 'MIT License'
							url 'http://opensource.org/licenses/mit-license.php'
							distribution 'repo'
						}
					}

					developers {
						developer {
							id 'SquidDev'
							name 'SquidDev'
						}
					}
				}
			}
		} else {
			mavenLocal()
		}
	}
}
